plugins {
    id 'java'
    id 'checkstyle'
    id 'io.freefair.lombok' version '6.0.0-m2' apply false
    id 'org.springframework.boot' version '2.7.7' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '3.1.2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.0"
        }
    }
}

checkstyle {
    toolVersion = '8.42'
    ignoreFailures = false
    configFile = file("config/checkstyle/checkstyle.xml")
    reportsDir = file("${buildDir}/checkstyle-output")
    maxErrors = 0
    maxWarnings = 0
}

jib {
    from {
        image = "adoptopenjdk/openjdk11:alpine"
    }
    to {
        image = "kimstz0/blue-delivery"
        tags = ["latest"]
    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
        jvmFlags = ["-Dspring.profiles.active=prod", "-javaagent:elastic-apm-agent-1.25.0.jar",
                    "-Delastic.apm.service_name=blue-delivery", "-Delastic.apm.server_url=http://apm:8200",
                    "-Delastic.apm.application_packages=com.bluedelivery"]
    }
}
